#####################
# PointEvaluator #
#####################


mutable struct PointEvaluator{Tv <: Real, UT <: Union{Unknown, Integer}, KFT <: Function}
    u_args::Array{UT,1}
    ops_args::Array{DataType,1}
    kernel::KFT
    BE_args
    L2G         
    eval_selector    
    evaluator
    parameters::Dict{Symbol,Any}
end

default_peval_kwargs()=Dict{Symbol,Tuple{Any,String}}(
    :name => ("PointEvaluator", "name for operator used in printouts"),
    :resultdim => (0, "dimension of result field (default = length of operators)"),
    :params => (nothing, "array of parameters that should be made available in qpinfo argument of kernel function"),
    :verbosity => (0, "verbosity level")
)


function PointEvaluator(kernel, u_args, ops_args, sol = nothing; Tv = Float64, kwargs...)
    parameters=Dict{Symbol,Any}( k => v[1] for (k,v) in default_peval_kwargs())
    _update_params!(parameters, kwargs)
    @assert length(u_args) == length(ops_args)
    PE = PointEvaluator{Tv, typeof(u_args[1]), typeof(kernel)}(u_args, ops_args, kernel, nothing, nothing, nothing, nothing, parameters)
    if sol !== nothing
        initialize!(PE, sol)
    end
    return PE
end

function PointEvaluator(kernel, oa_args::Array{<:Tuple{Union{Unknown,Int}, DataType},1}, sol = nothing; kwargs...)
    u_args = [oa[1] for oa in oa_args]
    ops_args = [oa[2] for oa in oa_args]
    return PointEvaluator(kernel, u_args, ops_args, sol; kwargs...)
end

function PointEvaluator(oa_args::Array{<:Tuple{Union{Unknown,Int}, DataType},1}, sol = nothing; kwargs...)
    u_args = [oa[1] for oa in oa_args]
    ops_args = [oa[2] for oa in oa_args]
    return PointEvaluator(standard_kernel, u_args, ops_args, sol; kwargs...)
end

function initialize!(O::PointEvaluator{T, UT}, sol; time = 0, kwargs...) where {T, UT}
    _update_params!(O.parameters, kwargs)
    if UT <: Integer
        ind_args = O.u_args
    elseif UT <: Unknown
        ind_args = [findfirst(==(u), sol.tags) for u in O.u_args]
    end
    FES_args = [sol[j].FES for j in ind_args]
    nargs = length(FES_args)
    FETypes_args = [eltype(F) for F in FES_args]
    xgrid = FES_args[1].xgrid
    EGs = xgrid[UniqueCellGeometries]
    AT = ON_CELLS
    gridAT = ExtendableFEMBase.EffAT4AssemblyType(get_AT(FES_args[1]), AT)
    xgrid = FES_args[1].xgrid
    itemregions = xgrid[CellRegions]
    itemgeometries = xgrid[CellGeometries]


    O.BE_args = Array{Array{<:FEEvaluator,1},1}([])
    O.L2G = []
    for EG in EGs
        ## FE basis evaluator for EG
        push!(O.BE_args, [FEEvaluator(FES_args[j], O.ops_args[j], QuadratureRule{T, EG}(0); AT = AT) for j in 1 : nargs])

        ## L2G map for EG
        push!(O.L2G, L2GTransformer(EG, xgrid, gridAT))
    end

    ## parameter structure
    QPinfo = QPInfos(xgrid; time = time)

    ## prepare input args
    op_lengths_args = [size(O.BE_args[1][j].cvals,1) for j = 1 : nargs]
    op_offsets_args = [0]
    append!(op_offsets_args, cumsum(op_lengths_args))
    input_args = zeros(T, op_offsets_args[end])

    FEATs_args = [ExtendableFEMBase.EffAT4AssemblyType(get_AT(FES_args[j]), AT) for j = 1 : nargs]
    itemdofs_args::Array{Union{Adjacency{Int32}, SerialVariableTargetAdjacency{Int32}},1} = [FES_args[j][Dofmap4AssemblyType(FEATs_args[j])] for j = 1 : nargs]
    kernel = O.kernel

    function eval_selector(item)
        return findfirst(==(itemgeometries[item]), EGs)
    end

    function _evaluate!(
        result,
        BE_args::Array{<:FEEvaluator,1},
        L2G::L2GTransformer,
        xref,
        item # cell used to evaluate local coordinates
        )
    
        for id = 1 : nargs
            # update basis evaluations at xref
            ExtendableFEMBase.relocate_xref!(BE_args[id], xref)

            # update operator eveluation on item
            update_basis!(BE_args[id], item)
        end
        
        # update QPinfo
        QPinfo.item = item
        QPinfo.region = itemregions[item]
        QPinfo.xref = xref
        update_trafo!(L2G, item)
        eval_trafo!(QPinfo.x, L2G, xref)

        # evaluate operator
        fill!(input_args,0)
        for id = 1 : nargs
            for j = 1 : size(BE_args[id].cvals, 2)
                dof_j = itemdofs_args[id][j, item]
                for d = 1 : op_lengths_args[id]
                    input_args[d + op_offsets_args[id]] += sol[id][dof_j] * BE_args[id].cvals[d, j, 1]
                end
            end
        end

        ## evaluate kernel
        kernel(result, input_args, QPinfo)
        
        return nothing
    end
    O.evaluator = _evaluate!
    O.eval_selector = eval_selector

    return nothing
end

function evaluate!(
    result,
    PE::PointEvaluator,
    xref, 
    item
    )

    ## find cell geometry id
    j = PE.eval_selector(item)

    ## evaluate
    PE.evaluator(result, PE.BE_args[j], PE.L2G[j], xref, item)
end



function eval_func(PE::PointEvaluator)
    return (result,xref,item) -> evaluate!(result,PE,xref,item)
end